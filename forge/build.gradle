plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    forge {
        mixinConfig "$project.archives_name" + ".mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

def generatedResources = file("src/generated")

sourceSets {
    main {
        resources.srcDir generatedResources
    }
}

repositories {
    maven { url "https://maven.minecraftforge.net/" }
}

loom {
    runs {
        data {
            data()
            programArgs "--all", "--mod", project.archives_name
            programArgs "--output", generatedResources.absolutePath
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mod_id', project.archives_name
    inputs.property 'mod_name', project.mod_name
    inputs.property 'mod_description', project.mod_description
    inputs.property 'mod_authors', project.mod_authors
    inputs.property 'mod_license', project.mod_license
    inputs.property 'mod_sources', project.mod_sources
    inputs.property 'mod_issue_tracker', project.mod_issue_tracker
    inputs.property 'minecraft_version', project.minecraft_version
    inputs.property 'forge_version', project.forge_version
    inputs.property 'minimum_minecraft_version', project.minimum_minecraft_version

    def majorForgeVersion = project.forge_version.replaceAll(/.*-(\d+)\..*/, '$1')
    println("Using Forge version: $majorForgeVersion")

    def majorMinecraftVersion
    if (project.minimum_minecraft_version != "0") {
        println("Using minimum Minecraft version: ${project.minimum_minecraft_version}")
        majorMinecraftVersion = project.minimum_minecraft_version
    } else {
        majorMinecraftVersion = project.minecraft_version.replaceAll(/(\d+\.\d+)\..*/, '$1')
        println("Using Minecraft version: $majorMinecraftVersion")
    }

    filesMatching('META-INF/mods.toml') {
        expand(
                version: project.version,
                mod_id: project.archives_name,
                mod_name: project.mod_name,
                mod_description: project.mod_description,
                mod_authors: project.mod_authors,
                mod_license: project.mod_license,
                mod_sources: project.mod_sources,
                mod_issue_tracker: project.mod_issue_tracker,
                minecraft_version: majorMinecraftVersion,
                forge_version: majorForgeVersion
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}